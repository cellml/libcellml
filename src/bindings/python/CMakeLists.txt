# Copyright libCellML Contributors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Add swig interface files
# Note: These filenames double as (generated .so file) module names!
set(SWIG_INTERFACE_SRCS
    ../interface/analyser.i
    ../interface/analyserequation.i
    ../interface/analyserequationast.i
    ../interface/analyserexternalvariable.i
    ../interface/analysermodel.i
    ../interface/analyservariable.i
    ../interface/annotator.i
    ../interface/component.i
    ../interface/componententity.i
    ../interface/entity.i
    ../interface/enums.i
    ../interface/generator.i
    ../interface/generatorprofile.i
    ../interface/importer.i
    ../interface/importsource.i
    ../interface/importedentity.i
    ../interface/issue.i
    ../interface/logger.i
    ../interface/model.i
    ../interface/namedentity.i
    ../interface/parentedentity.i
    ../interface/parser.i
    ../interface/printer.i
    ../interface/reset.i
    ../interface/strict.i
    ../interface/types.i
    ../interface/units.i
    ../interface/validator.i
    ../interface/variable.i
    ../interface/version.i
)

# Put the Intermediary files generated by SWIG into their own directory.
set(SWIG_GENERATED_INTERMEDIARIES_FILES_DIR ${CMAKE_CURRENT_BINARY_DIR}/swig_intermediaries)

# Set the location of the libcellml Python package directory.
set(LIBCELLML_PYTHON_PACKAGE_DIR ${CMAKE_CURRENT_BINARY_DIR}${CONFIG_DIR_SUFFIX}/libcellml)
set(_SWIG_GENERATED_PYTHON_FILES_DIR ${CMAKE_CURRENT_BINARY_DIR}/swig_generated_python_files)
set(FINALISE_PYTHON_BINDINGS_ARGUMENTS -DLIBCELLML_PYTHON_PACKAGE_DIR=${LIBCELLML_PYTHON_PACKAGE_DIR})

if(LIBCELLML_PYTHON_COVERAGE)
  set(PYTHON_COVERAGE_WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR} CACHE INTERNAL "The directory for the Python coverage script to run in.")
endif()

# Configure for C++
set_source_files_properties(${SWIG_INTERFACE_SRCS} PROPERTIES
  CPLUSPLUS ON
  INCLUDE_DIRECTORIES "${PROJECT_SOURCE_DIR}/src/api;${PROJECT_BINARY_DIR}/src/api"
)

set(SETUP_PY_PACKAGE_FILES "'\${TARGET_FILE_NAME_cellml}'")
list(APPEND FINALISE_PYTHON_BINDINGS_ARGUMENTS -DTARGET_FILE_NAME_cellml=$<TARGET_FILE_NAME:cellml>)
if (UNIX AND NOT APPLE AND LIBCELLML_BUILD_SHARED)
  list(APPEND SETUP_PY_PACKAGE_FILES "'\${TARGET_SONAME_FILE_NAME_cellml}'")
  list(APPEND FINALISE_PYTHON_BINDINGS_ARGUMENTS -DTARGET_SONAME_FILE_NAME_cellml=$<TARGET_SONAME_FILE_NAME:cellml>)
  set(_GENERATOR_EXPRESSION_LIBCELLML_SONAME $<TARGET_SONAME_FILE:cellml>)
endif()
if(MSVC AND TARGET zlib AND ZLIB_TARGET_TYPE STREQUAL "SHARED_LIBRARY")
  list(APPEND SETUP_PY_PACKAGE_FILES "'\${TARGET_FILE_NAME_zlib}'")
  list(APPEND FINALISE_PYTHON_BINDINGS_ARGUMENTS -DTARGET_FILE_NAME_zlib=$<TARGET_FILE_NAME:z>)
  set(_GENERATOR_EXPRESSION_ZLIB_FILE $<TARGET_FILE:z>)
endif ()
if(MSVC AND HAVE_LIBXML2_CONFIG AND LIBXML2_TARGET_TYPE STREQUAL "SHARED_LIBRARY")
  list(APPEND SETUP_PY_PACKAGE_FILES "'\${TARGET_FILE_NAME_libxml2}'")
  list(APPEND FINALISE_PYTHON_BINDINGS_ARGUMENTS -DTARGET_FILE_NAME_libxml2=$<TARGET_FILE_NAME:${LIBXML2_TARGET_NAME}>)
  set(_GENERATOR_EXPRESSION_LIBXML2_FILE $<TARGET_FILE:${LIBXML2_TARGET_NAME}>)
endif ()

set(SWIG_PYTHON_BINDINGS_TARGETS)
# Create bindings for each interface file
foreach(SWIG_INTERFACE_SRC ${SWIG_INTERFACE_SRCS})
  get_filename_component(MODULE_TARGET ${SWIG_INTERFACE_SRC} NAME_WE)
  swig_add_library(${MODULE_TARGET}
    LANGUAGE python
    OUTPUT_DIR ${_SWIG_GENERATED_PYTHON_FILES_DIR}
    OUTFILE_DIR ${SWIG_GENERATED_INTERMEDIARIES_FILES_DIR}
    SOURCES ${SWIG_INTERFACE_SRC}
  )
  target_link_libraries(${MODULE_TARGET} PRIVATE cellml)

  if(SWIG_VERSION VERSION_GREATER_EQUAL 4.1.0)
    set_target_properties(${MODULE_TARGET} PROPERTIES SWIG_COMPILE_OPTIONS -flatstaticmethod)
  endif()

  if(FIND_PYTHON_DEVELOPMENT_MODULE)
    set(_PYTHON_LINK_TARGET Python::Module)
  else()
    set(_PYTHON_LINK_TARGET Python::Python)
  endif()

  if (APPLE)
    # Specifically allow dynamic lookup, here we are really targeting Python symbols.
    set_target_properties(${MODULE_TARGET} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
    # But, we do need to know about the Python include directories.
    target_include_directories(${MODULE_TARGET} PRIVATE ${Python_INCLUDE_DIRS})
  elseif (UNDEFINED_SYMBOLS_ALLOWED)
    # Linker allows undefined symbols, so let's not link Python.
    # But, we do need to know about the Python include directories.
    target_include_directories(${MODULE_TARGET} PRIVATE ${Python_INCLUDE_DIRS})
  else()
    target_link_libraries(${MODULE_TARGET} PRIVATE ${_PYTHON_LINK_TARGET})
  endif()
  # Disable use of Debug Python libraries when not present on Windows
  # See http://stackoverflow.com/questions/11311877/creating-a-dll-from-a-wrapped-cpp-file-with-swig
  if(WIN32)
    if(NOT PYTHON_DEBUG_LIBRARY)
      target_compile_definitions(${MODULE_TARGET} PRIVATE $<$<CONFIG:Debug>:SWIG_PYTHON_INTERPRETER_NO_DEBUG>)
    endif()
  endif()
  if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang"
     OR "${CMAKE_CXX_COMPILER_ID}" STREQUAL "AppleClang")
    set_target_properties(${MODULE_TARGET} PROPERTIES
      COMPILE_OPTIONS "-Wno-deprecated-declarations"
    )
  endif()
  if(APPLE)
    set(RPATH_ORIGIN "@loader_path/.")
  elseif(UNIX)
    set(RPATH_ORIGIN "\$ORIGIN")
  else()
    set(RPATH_ORIGIN "no_rpath")
  endif()
  set_target_properties(${MODULE_TARGET} PROPERTIES
    CXX_VISIBILITY_PRESET hidden
    BUILD_WITH_INSTALL_RPATH TRUE
    INSTALL_RPATH "${RPATH_ORIGIN}"
    FOLDER bindings/python
    RUNTIME_OUTPUT_DIRECTORY ${LIBCELLML_PYTHON_PACKAGE_DIR}
    LIBRARY_OUTPUT_DIRECTORY ${LIBCELLML_PYTHON_PACKAGE_DIR}
  )
  list(APPEND SWIG_PYTHON_BINDINGS_TARGETS ${MODULE_TARGET})
  list(APPEND SETUP_PY_PACKAGE_FILES "'\${TARGET_FILE_NAME_${MODULE_TARGET}}'")
  list(APPEND FINALISE_PYTHON_BINDINGS_ARGUMENTS -DTARGET_FILE_NAME_${MODULE_TARGET}=$<TARGET_FILE_NAME:${MODULE_TARGET}>)
endforeach()

add_custom_target(python_bindings ALL DEPENDS ${SWIG_PYTHON_BINDINGS_TARGETS})

string(TOLOWER ${PROJECT_NAME} PYPI_PACKAGE_NAME)
set(PYPI_PACKAGE_VERSION ${PROJECT_VERSION})
set(PYPI_PACKAGE_URL ${PROJECT_URL})
if (PROJECT_DEVELOPER_VERSION MATCHES "-rc\.")
  string(REPLACE "-rc." ".rc" PYPI_PACKAGE_DEVELOPER_VERSION ${PROJECT_DEVELOPER_VERSION})
elseif (PROJECT_DEVELOPER_VERSION MATCHES "-dev\.")
    string(REPLACE "-dev." ".dev" PYPI_PACKAGE_DEVELOPER_VERSION ${PROJECT_DEVELOPER_VERSION})
endif()
set(SETUP_PY_PACKAGE_FILES_STR "${SETUP_PY_PACKAGE_FILES}")
string(REPLACE ";" ", " SETUP_PY_PACKAGE_FILES_STR "${SETUP_PY_PACKAGE_FILES_STR}")
set(SETUP_PRE_GEN_PY_FILE "${CMAKE_CURRENT_BINARY_DIR}/pre_gen.setup.py")
configure_file(pre_gen.setup.in.py ${SETUP_PRE_GEN_PY_FILE} @ONLY)

set(FINALISE_PYTHON_BINDINGS_SCRIPT ${CMAKE_CURRENT_BINARY_DIR}/finalise_python_package.cmake)
configure_file(${PROJECT_SOURCE_DIR}/cmake/finalise_python_package.in.cmake ${FINALISE_PYTHON_BINDINGS_SCRIPT} @ONLY)
add_custom_command(TARGET python_bindings POST_BUILD
  COMMAND ${CMAKE_COMMAND} ${FINALISE_PYTHON_BINDINGS_ARGUMENTS} -P ${FINALISE_PYTHON_BINDINGS_SCRIPT}
  COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_FILE:cellml> ${_GENERATOR_EXPRESSION_LIBCELLML_SONAME} ${_GENERATOR_EXPRESSION_ZLIB_FILE} ${_GENERATOR_EXPRESSION_LIBXML2_FILE} ${LIBCELLML_PYTHON_PACKAGE_DIR}
  COMMAND ${CMAKE_COMMAND} -E copy_directory ${_SWIG_GENERATED_PYTHON_FILES_DIR} ${LIBCELLML_PYTHON_PACKAGE_DIR}
  DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/__init__.py ${CMAKE_CURRENT_SOURCE_DIR}/README.rst ${SETUP_PRE_GEN_PY_FILE}
  COMMENT "Finalise preparation of libCellML Python bindings."
)

# Add new paths to make clean
set_property(DIRECTORY APPEND PROPERTY ADDITIONAL_MAKE_CLEAN_FILES ${LIBCELLML_PYTHON_PACKAGE_DIR} ${SWIG_GENERATED_INTERMEDIARIES_FILES_DIR})

if(SKBUILD)
  install(TARGETS ${SWIG_PYTHON_BINDINGS_TARGETS}
    COMPONENT wheel
    DESTINATION libcellml
    EXCLUDE_FROM_ALL
  )
  install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/__init__.py
    COMPONENT wheel
    EXCLUDE_FROM_ALL
    DESTINATION libcellml
  )
  install(DIRECTORY ${_SWIG_GENERATED_PYTHON_FILES_DIR}/
    COMPONENT wheel
    EXCLUDE_FROM_ALL
    DESTINATION libcellml
  )
  add_custom_target(install-wheel
    ${CMAKE_COMMAND}
    -DCMAKE_INSTALL_COMPONENT=wheel
    -P "${PROJECT_BINARY_DIR}/cmake_install.cmake"
    DEPENDS ${SWIG_PYTHON_BINDINGS_TARGETS}
  )
endif()
